pluginManagement {
    includeBuild('gradle/projectPlugins')

    repositories {
        mavenLocal()
        gradlePluginPortal()
        maven {
            name 'xomda packages'
            url 'https://maven.pkg.github.com/xomda/xomda'
        }
    }
}

plugins {
    // Apply the foojay-resolver plugin to allow automatic download of JDKs
    id 'org.gradle.toolchains.foojay-resolver-convention' version '0.8.0'
}

void includeProjectsFrom(File file) {
    file.listFiles().each {
        if (!it.isDirectory()) return
        if (new File(it, 'build.gradle').exists()) {
            include(rootProject.projectDir.relativePath(it).replace('/', ':'))
        }
    }
}

rootProject.name = 'xomda-test'

includeProjectsFrom(rootProject.projectDir)
includeProjectsFrom(file("test-multi-project"))
