pluginManagement {
    includeBuild('gradle/projectPlugins')

    repositories {
        mavenLocal()
        mavenCentral()
        gradlePluginPortal()
        maven {
            name 'xomda packages'
            url 'https://maven.pkg.github.com/xomda/xomda'

            credentials {
                username = System.getenv("USERNAME")
                password = System.getenv("TOKEN")
            }
        }
    }
}

plugins {
    // Apply the foojay-resolver plugin to allow automatic download of JDKs
    id 'org.gradle.toolchains.foojay-resolver-convention' version '0.9.0'
}

void includeProjectsFrom(File file) {
    file.listFiles().each {
        if (!it.isDirectory()) return
        if (new File(it, 'build.gradle').exists()) {
            include(rootProject.projectDir.relativePath(it).replace('/', ':'))
        }
    }
}

rootProject.name = 'xomda-test-project'

includeProjectsFrom(rootProject.projectDir)
includeProjectsFrom(file("test-multi-project"))
